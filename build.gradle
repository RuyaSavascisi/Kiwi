plugins {
    id "io.github.goooler.shadow" version "8.1.7"
    id 'fabric-loom' version '1.6.+'
    id 'maven-publish'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.minecraft_version + "-Fabric-" + project.mod_version
group = project.maven_group
var realVersion = project.mod_version + '+fabric'

repositories {
    maven { url "https://maven.parchmentmc.org" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.Snownee"
        }
    }
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

java.withSourcesJar()

sourceSets {
    main.resources.srcDirs += "$rootDir/src/generated/resources"
}

loom {
    accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")
    mixin {
        defaultRefmapName = "${project.mod_id}.refmap.json"
    }
    runs {
        //
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagenClient"
        //
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            runDir "build/datagen"
        }
    }
}

configurations {
    shadowCommon
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    implementation shadowCommon("org.yaml:snakeyaml:${project.snakeyaml_version}")
    implementation shadowCommon("com.ezylang:EvalEx:${project.evalex_version}")
    implementation shadowCommon(project(':annotation')) {
        transitive(false)
    }
    implementation shadowCommon(project(':annotation-processor')) {
        transitive(false)
    }
    annotationProcessor project(':annotation-processor')

    implementation "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
    inputs.property "version", realVersion

    filesMatching("fabric.mod.json") {
        expand "version": realVersion
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 21

    it.options.compilerArgs.add("-Akiwi.clientOnlyMod")
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    // withSourcesJar()
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    it.exclude([
            "/assets/${project.mod_id}/blockstates",
            "/assets/${project.mod_id}/models",
            "/assets/${project.mod_id}/lang/en_us.existing.json",
            "/assets/kaleidos",
            "/snownee/${project.mod_id}/test",
            "/data",
            "/.cache",
            "/third_party"
    ])
}

jar {
}

shadowJar {
    relocate "org.yaml.snakeyaml", "${project.group}.shadowed.org.yaml.snakeyaml"
    relocate "com.ezylang.evalex", "${project.group}.shadowed.com.ezylang.evalex"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set('shadow')
}

remapJar {
    dependsOn(shadowJar)
    inputFile.set(shadowJar.archiveFile)
    archiveClassifier.set("")

    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

unifiedPublishing {
    project {
        displayName = "[Fabric $project.supported_version] $project.mod_version"
        version = realVersion // Optional, Inferred from project by default
        changelog = file("CHANGELOG.md").exists() ? file("CHANGELOG.md").text : "" // Optional, in markdown format
        releaseType = project.release_type // Optional, use "release", "beta" or "alpha"
        gameVersions = [project.minecraft_version, "1.21.1"]
        gameLoaders = ["fabric", "quilt"]

        mainPublication tasks.remapJar // Declares the publicated jar

        if (System.getenv("CURSE_TOKEN") != null) {
            curseforge {
                token = System.getenv("CURSE_TOKEN")
                id = "549404" // Required, must be a string, ID of CurseForge project

                relations { // Optional, Inferred from the relations above by default
                    depends "fabric-api"
                    optional "cloth-config"
                }
            }
        }

        if (System.getenv("MODRINTH_TOKEN") != null) {
            modrinth {
                token = System.getenv("MODRINTH_TOKEN")
                id = "ufdDoWPd" // Required, must be a string, ID of Modrinth project

                relations { // Optional, Inferred from the relations above by default
                    depends "fabric-api"
                    optional "cloth-config"
                }
            }
        }
    }
}
